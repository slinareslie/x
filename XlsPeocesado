Private Sub TextBox1_Change()
    Dim criterio As String
    Dim columna As Integer
    Dim celda As Range
    Dim rngDatos As Range
    Dim fila As Range
    Dim textoCelda As String
    Dim textoFiltro As String

    ' Determinar la columna según ComboBox
    Select Case Me.ComboBox1.Value
        Case "Num Correlativo": columna = 1
        Case "Num Expediente": columna = 2
        Case "Tipo de Documento": columna = 3
        Case "N° Documento Acumulador": columna = 4
        Case "Nombre": columna = 5
        Case "COD/RUC": columna = 6
        Case Else: columna = 1
    End Select

    ' Normalizar el criterio ingresado
    criterio = NormalizarTexto(Me.TextBox1.Text)

    ' Mostrar todas las filas antes de aplicar el filtro
    Me.Rows.Hidden = False

    ' Rango de datos (sin encabezado)
    Set rngDatos = Me.Range("A7", Me.Cells(Me.Rows.Count, "A").End(xlUp)).EntireRow

    Application.ScreenUpdating = False
    For Each fila In rngDatos
        textoCelda = NormalizarTexto(fila.Cells(1, columna).Text)
        If criterio <> "" Then
            If InStr(1, textoCelda, criterio, vbTextCompare) = 0 Then
                fila.EntireRow.Hidden = True
            End If
        End If
    Next fila
    Application.ScreenUpdating = True
End Sub
Function NormalizarTexto(ByVal texto As String) As String
    Dim acentos, sinAcentos, i As Integer
    acentos = "ÁÉÍÓÚÜÑáéíóúüñ"
    sinAcentos = "AEIOUUNaeiouun"

    texto = LCase(texto)
    For i = 1 To Len(acentos)
        texto = Replace(texto, Mid(acentos, i, 1), Mid(sinAcentos, i, 1))
    Next i
    NormalizarTexto = texto
End Function
